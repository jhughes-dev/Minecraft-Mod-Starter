buildscript {
    if (rootProject.hasProperty("kotlin_version")) {
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${rootProject.kotlin_version}"
        }
    }
}

plugins {
    id "architectury-plugin" version "${architectury_plugin_version}"
    id "dev.architectury.loom" version "${architectury_loom_version}" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
        runConfigs.configureEach {
            runDir = "../run"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.neoforged.net/releases/" }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    if (rootProject.hasProperty("mod_language") && rootProject.mod_language == "kotlin") {
        apply plugin: "org.jetbrains.kotlin.jvm"
        kotlin {
            jvmToolchain(21)
        }
    }
}
